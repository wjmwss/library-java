def projectName = 'itcp-sugar-component-convert-all'

version = projectVersion

dependencies {
    // framework
    implementation("$spring_boot_webmvc")
    testImplementation("$spring_boot_webmvc")

    // tool
    implementation(project("$itcp_sugar_tool"))
    implementation(project("$itcp_sugar_framework_spring_tool_common"))
    api(project("$itcp_sugar_component_convert_spring"))
    api(project("$itcp_sugar_component_convert_jackson"))
    api(project("$itcp_sugar_component_convert_fastjson"))
    api(project("$itcp_sugar_component_convert_mapstruct"))
    annotationProcessor("$mapstruct_processor")
    annotationProcessor("$lombok_mapstruct_binding")

    // test
    testImplementation(project("$itcp_sugar_tool"))
    testImplementation(project("$itcp_sugar_component_convert_jackson"))
    testImplementation(project("$itcp_sugar_component_convert_mapstruct"))
    testAnnotationProcessor("$mapstruct_processor")
    testAnnotationProcessor("$lombok_mapstruct_binding")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = projectCharset
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding projectCharset
        version true
        title projectName
    }
}

publishing {
    publications {
        publishApp(MavenPublication) {
            groupId group
            artifactId jar.baseName
            version project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            credentials {
                username projectNexusUsername
                password projectNexusPassword
            }
            url = projectNexusUrl
            allowInsecureProtocol = true
        }
    }
}