plugins {
    id 'maven-publish'
}

def projectName = 'itcp-sugar-redisson'

version = '1.0'

dependencies {
    // framework
    implementation("$spring_boot_starter")
    implementation("$spring_boot_starter_aop")

    // database
    api("$redisson")

    // tool
    implementation(project(':itcp-sugar-tools'))
    annotationProcessor("$spring_boot_annotation_processor")
    testAnnotationProcessor("$spring_boot_annotation_processor")

    // test
    testImplementation 'org.testng:testng:7.1.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = projectCharset
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding projectCharset
        version true
        title projectName
    }
}

publishing {
    publications {
        publishApp(MavenPublication) {
            groupId group
            artifactId jar.baseName
            version project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            credentials {
                username publishUsername
                password publishPassword
            }
            url repositoryUrl
            allowInsecureProtocol = true
        }
    }
}